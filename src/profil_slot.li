///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Compiler                               //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name      := PROFIL_SLOT;

  - copyright := "2003-2007 Benoit Sonntag";
  
  - author    := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment   := "Method with costumization";
  
Section Inherit
  
  + parent_profil:Expanded PROFIL; 
  
Section Public
    
  - slot:SLOT <- slot_code;
  
  - is_interrupt:BOOLEAN <- slot_code.id_section.is_interrupt;  
  - is_external:BOOLEAN  <- slot_code.id_section.is_external;  

  + slot_code:SLOT_CODE;
    
  + is_context_sensitive:BOOLEAN; 
  // BSBS: Le bloc passÃ© en argument peux ne pas etre context sensitive
  // Et puis, cet stat peu changer dans le temps...
    
  - set_context_sensitive <-
  (
    is_context_sensitive := TRUE;
  );

  //
  // Creation.
  //
  
  - make s:SLOT_CODE 
  with (typ_self:TYPE_FULL,call_lst:FAST_ARRAY[EXPR]) 
  verify is_first:BOOLEAN :FAST_ARRAY[WRITE] <-
  ( + loc:LOCAL;
    + item_lst:FAST_ARRAY[ARGUMENT];       
    + idx_result:INTEGER;
    + types:FAST_ARRAY[TYPE_FULL];    
    + result:FAST_ARRAY[WRITE];
    
    PROFIL_LIST.add Self;
    
    (s.id_section.is_external).if {
      name := s.name;
    } else {
      name := ALIAS_STR.get_intern (s.name);    
    };
    slot_code := s;
    type_self := typ_self;
    //
    context := CONTEXT_SLOT.push (slot_code.position) 
    slot Self self typ_self;
    //
    code := list_current;
    //
    // Arguments.    
    item_lst := s.arguments;    
    argument_list := FAST_ARRAY[LOCAL].create_with_capacity (item_lst.count); 
    (item_lst.lower).to (item_lst.upper) do { j:INTEGER;
      argument_list.add_last (
	item_lst.item j.to_local
      );
    };
    ((s.id_section.is_external) && {argument_list.count > 1}).if {
      (argument_list.lower+1).to (argument_list.upper) do { j:INTEGER;      
	loc := argument_list.item j;
	loc.set_ensure_count 1;
	loc.write (loc.position) value (
	  EXTERNAL_C.create (loc.position) text "/* External slot */"
	  access NULL persistant FALSE type (loc.type)
	);
      };
    };    
    
    // Results
    (s.result_type.raw != TYPE_VOID).if {
      types := s.results_type;      
      (types != NULL).if {  
	result_list := FAST_ARRAY[LOCAL].create_with_capacity (types.count);		
	idx_result := 1;
	(types.lower).to (types.upper) do { k:INTEGER;	  
	  typ := types.item k.fix;	
	  loc := typ.get (s.position) result idx_result;
	  result_list.add_last loc;
	  idx_result := idx_result + 1;
	};
      };
      result_last := result_type.fix.get (s.position) result idx_result;
    };
    //
    result := write_argument call_lst;
    //        
    slot_code.create_code is_first;               
    CONTEXT.pop;    
    //
    result
  );
      
  //
  // Execute.
  //
  
  - remove_inline <-
  (
    parent_profil.remove_inline;
    slot_code.remove_profil Self;    
  );
  
  - remove <-
  ( 
    parent_profil.remove;
    slot_code.remove_profil Self;            
  );
  
  //
  // Genere.
  //
  
  - is_static:BOOLEAN <- (! slot.id_section.is_interrupt) && {! slot.id_section.is_external};
