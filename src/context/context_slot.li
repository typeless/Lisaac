///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Compiler                               //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name      := CONTEXT_SLOT;

  - copyright := "2003-2007 Benoit Sonntag";
  
  - author    := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment   := "Create a new context for analyser";
      
Section Inherit
  
  - parent_context:CONTEXT := CONTEXT;
  
Section Private
  
  - free_list:FAST_ARRAY[CONTEXT_SLOT] := FAST_ARRAY[CONTEXT_SLOT].create_with_capacity 16;
  
  + old_local_lower:INTEGER;
  + old_result_lower:INTEGER;
  + old_list:LIST;
  + old_profil_slot:PROFIL_SLOT;
  + old_profil:PROFIL;
  
Section Public
  
  - limit_context:INTEGER <- 0;
  
  - push p:POSITION slot prof:PROFIL_SLOT self type_self:TYPE_FULL generic rec:TYPE :LOCAL <-
  (
    deferred;
    NULL
  );
  
  - push p:POSITION slot prof:PROFIL_SLOT :LOCAL <-
  ( + result:CONTEXT_SLOT;
    
    (free_list.is_empty).if {
      result := CONTEXT_SLOT.clone;
    } else {
      result := free_list.last;
      free_list.remove_last;
    };
    result.make p slot prof
  );
  
  - make pos:POSITION slot prof:PROFIL_SLOT :LOCAL <-
  ( + res:LOCAL;
    
    // Save context.
    old_local_lower  := stack_local_lower;
    old_result_lower := stack_result_lower;
    old_list         := list_current;
    old_profil_slot  := profil_slot;
    old_profil       := profil;
    // Creation.
    stack_local_lower  := stack_local.upper + 1;
    stack_result_lower := stack_result.upper + 1; 
    list_current       := LIST.create pos;
    profil := profil_slot := prof;    
    //
    (debug_level_option != 0).if {
      // Debug mode : Add context local.
      res := TYPE_CONTEXT.default.new_local pos name (ALIAS_STR.variable_context) style '+';
      res.set_ensure_count 1;      
      list_current.add_last (PUSH.create pos context res first TRUE);
    };
    // Push context.
    stack.add_last Self;
    res
  );
  
  - pop <-
  (    
    // Restore Context.
    ITM_OBJECT.pop_stack_until stack_local_lower;
    stack_local_lower := old_local_lower;
    stack_result.remove_since stack_result_lower;
    stack_result_lower:= old_result_lower;    
    list_current      := old_list;
    profil_slot       := old_profil_slot;
    profil            := old_profil;    
    // Pop context.
    stack.remove_last;
  );
  