///////////////////////////////////////////////////////////////////////////////
//                            Lisaac Installer                               //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////  

// file LIP : LIsaac Path directory and make LIsaac Project system.

Section Private
  
  //
  // Compiler variables.
  //
  
  // File information.
  + lisaac:STRING;      // is environment variable value (auto-loading).
  + input_file:STRING;  // is input file name value without extension (auto-loading, if possible).
    
  // Debug information.
  + debug_level:INTEGER := 15;
  + debug_with_code:BOOLEAN := TRUE; 
  + is_all_warning:BOOLEAN;
    
  // Optimization.
  + is_optimization:BOOLEAN;
  + inline_level:INTEGER := 15;
  
  // Generate code.
  + is_java:BOOLEAN;
  + is_cop:BOOLEAN; // Correct value after compilation.
  
  // Other.
  + is_statistic:BOOLEAN;
  + is_quiet:BOOLEAN;
  
  //
  // Other variables.
  //
  
  + option_gcc:STRING;  
  + lib_gcc:STRING;
  
  + target:STRING := "unix";
  
  //
  // Directory.
  //
  
  - standard_path <-
  // Standard library.
  ( 
    path (lisaac + "lib/*");    
  );
  
  //
  // Target path.
  //
  
  - unix_target <-
  (
    path (lisaac + "lib_os/unix/system/");
    path (lisaac + "lib_os/unix/file_system/");
    path (lisaac + "lib_os/unix/video/");
  );
  
  - windows_target <-
  (
    path (lisaac + "lib_os/unix/system/");
    path (lisaac + "lib_os/windows/file_system/");
    path (lisaac + "lib_os/unix/file_system/");  // BSBS: ??
    path (lisaac + "lib_os/windows/video/");
  );

  - dos_target <-
  (
    path (lisaac + "lib_os/unix/system/");
    path (lisaac + "lib_os/unix/file_system/"); // BSBS: ??    
    path (lisaac + "lib_os/dos/file_system/");
    path (lisaac + "lib_os/dos/video/");
  );
  
  - java_target <-
  (
    path (lisaac + "lib_os/java/system/");
    path (lisaac + "lib_os/java/file_system/");
  );
  
  - get_target <-
  (
    (target = "dos").if {
      dos_target;
    };
    (target = "windows").if {
      windows_target;
    };
    (target = "unix").if {
      unix_target;
    };
    (target = "java").if {
      java_target;
    };
    (target = "").if {
      "Target code needed.\n".print;
      exit;
    };
  );
      
  - add_lib lib:STRING <-
  (
    run "echo \"int main(){ return(1); }\" > __tmp__.c";    
    (run ("gcc __tmp__.c -o __tmp__ " + lib + " 2> /dev/null") = 0).if {
      lib_gcc := lib_gcc + " " + lib;
      run "rm __tmp__.c __tmp__";
    } else {
      "\nERROR: `" + lib + "' library for GCC not found.\n".print;
      run "rm __tmp__.c";
      exit;
    };    
  );
  
  - execute cmd:STRING <-
  (
    (! is_quiet).if {
      "run `".print;
      cmd.print;
      "'\n".print;
    };
    (run cmd != 0).if {
      "FAILURE!\n".print;
    };
  );
  
  //
  // Execute function.
  //
  
  - general_front_end <-
  (
    standard_path;
    get_target;
  );
  
  - general_back_end <-
  (
    (target = "java").if {
      (is_cop).if {
        "Warning: COP not yet implemented\n".print;
      };
      execute ("javac " + input_file + ".java");
    };
    (target = "dos").if {
      (is_cop).if {
        "Warning: COP not yet implemented\n".print;
      };
      execute ("gcc " + input_file + ".c -o " + input_file + ".exe " + option_gcc + lib_gcc); 
    };
    (target = "windows").if {
      (is_cop).if {
        "Warning: COP not yet implemented\n".print;
      };
      execute ("gcc " + input_file + ".c -o " + input_file + ".exe -lgdi32 " + option_gcc + lib_gcc);
    };
    (target = "unix").if {      
      (is_cop).if {
        lib_gcc := lib_gcc + " -lpthread";
      };
      execute ("gcc " + input_file + ".c -o " + input_file + " -lm -lX11 " + option_gcc + lib_gcc);
    };
  );
  
  - front_end <-
  // Executed by compiler, before compilation step.
  (
    general_front_end;
  );
  
  - back_end <-
  // Executed by compiler, after compilation step.
  (
    general_back_end;
  );
  
Section Public
  
  //
  // Debug information.
  // 
    
  - no_debug <-  
  // No debug information.
  (
    debug_level := 0;
    debug_with_code := FALSE;
  );
  
  - debug level:INTEGER <-
  // Fix debug level (default: 15)
  (
    ((level < 1) | (level > 20)).if {
      "Incorrect debug level.\n".print;
      exit;
    };
    debug_level := level;
  );
  
  - without_source <-
  // Debug mode without source code.
  (
    debug_with_code := FALSE;
  );
  
  - all_warning <-
  // All warning (deferred detect, ...).
  (
    is_all_warning := TRUE;
  );
  
  //
  // Optimization.
  //
  
  - boost <-
  // Full optimization.
  (
    no_debug;
    is_optimization := TRUE;
    option_gcc := option_gcc + " -O3 -fomit-frame-pointer";
  );
  
  - i level:INTEGER <-
  // Inlining level [1..5000] (default: 15)
  (
    ((level < 1) | (level > 5000)).if {
      "Incorrect inlining level.\n".print;
      exit;
    };
    inline_level := level;
  );
    
  //
  // Generate code.
  //
  
  - target idf:STRING <-
  // Target for backend (UNIX,WINDOWS,DOS,JAVA)
  (
    target := idf;
  );
  
  - java <-
  // Product Java code.
  (
    "Warning: Java code target is just for fun. Working progress...\n".print;
    is_java := TRUE;
    target := "java";
  );
  
  - gcc option:STRING <-
  // Add option for GCC.
  (
    option_gcc := option_gcc + " " + option;
  );
  
  //
  // Other.
  //
  
  - q <-
  // Quiet operation.
  (
    is_quiet := TRUE;
  );
  
  - s <-
  // Statistic information.
  (
    is_statistic := TRUE;
  );