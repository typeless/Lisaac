///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Library                                //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name    := DIRECTORY;


  - copyright   := "2003-2005 JÃ©rome Boutet, 2003-2007 Benoit Sonntag";
  
  - comment := "Basic Directory management";
  
Section Inherit
  
  - parent_entry:ENTRY := ENTRY;
  
Section DIRECTORY
  
  + list:LINKED_LIST[ENTRY];

  - alias:HASHED_DICTIONARY[ENTRY,ABSTRACT_STRING] := 
  HASHED_DICTIONARY[ENTRY,ABSTRACT_STRING].create;
  
Section Public
  
  //
  // Manager.
  //
  
  - remove_me <- remove_path ".";
  
  - remove p:ABSTRACT_STRING :BOOLEAN <-
  // Remove file or directory (WARNING: delete recursive)
  ( + dir:DIRECTORY;
    + i:INTEGER;
    + result:BOOLEAN;
    + e:ENTRY;
    
    e := get_entry p;
    (e != NULL).if {
      (e.is_directory).if {
        dir ?= e;
        result := dir.open;
        (result).if {
          i := dir.lower;        
          {(i > dir.upper) || {! result}}.until_do {
            result := remove (dir.item i.path);
            i := i + 1;
          };	        
          result.if {
            result := physical_remove (e.path);
          };
        };
      } else {
        result := physical_remove (e.path);
      };
      (result).if {
        // Remove Alias.
        alias.remove (e.path);        
      };
    };
    result
  );
  
  - move old_path:ABSTRACT_STRING to new_path:ABSTRACT_STRING :BOOLEAN <-
  ( + result:BOOLEAN;
    + e:ENTRY;
    
    string_tmp.copy path;
    string_tmp.add_last '/';
    string_tmp.append old_path;    
    reduce_path string_tmp;
    string_tmp2.copy path;
    string_tmp2.add_last '/';
    string_tmp2.append new_path;    
    reduce_path string_tmp2;
    e := alias.reference_at string_tmp;
    ((result := physical_move string_tmp to string_tmp2) && {e != NULL}).if {
      alias.remove string_tmp;
      e.set_path string_tmp2;
      alias.add e to (e.path);
    };
    result
  );
  
  - get_entry p:ABSTRACT_STRING :ENTRY <- 
  ( + new_entry,result:ENTRY;
    
    string_tmp2.copy path;
    string_tmp2.add_last '/';
    string_tmp2.append p;
    reduce_path string_tmp2;
    result := alias.reference_at string_tmp2;
    (result = NULL).if {
      new_entry := physical_get_entry string_tmp2;      
      (new_entry != NULL).if {
        alias.put new_entry to (new_entry.path);
        result := new_entry;
      };
    };
    
    result
  );  
  
  //
  // Indexing.
  //
  
  - open:BOOLEAN <-
  (
    deferred;
  )
  [
    +? {list != NULL};
  ];
  
  - is_open:BOOLEAN <- list != NULL;
  
  - lower:INTEGER := 1;
  // Minimum index.
  
  - upper:INTEGER <- 
  [
    {is_open} -? "Directory not open.";
  ]
  (
    list.upper
  );    
    
  // Maximum index.
  
  - valid_index index:INTEGER :BOOLEAN <- 
  // True when `index' is valid (ie. inside actual
  // bounds of the collection).
  [
    {is_open} -? "Directory not open.";
  ]
  ( 
    index.in_range lower to upper 
  )
  [
    +? {Result = index.in_range lower to upper};
    +? {list.count = Old list.count};
  ];
  
  //
  // Counting.
  //
  
  - count:INTEGER <- (upper - lower + 1);
  // Number of available indices.
  
  - is_empty:BOOLEAN <- count = 0;
  // Is collection empty ?
  
  //
  // Accessing.
  //
    
  - item i:INTEGER :ENTRY <-
  // Item at the corresponding index `i'. 
  [
    -? {valid_index i};
    {is_open} -? "Directory not open.";
  ]
  ( 
    list.item i
  );  
  
  - first:ENTRY <- 
  // The very `first' item.  
  [
    {is_open} -? "Directory not open.";
    -? {! is_empty};
  ]
  ( 
    item lower
  );
  
  
  - last:ENTRY <-
  // The `last' item.
  [
    {is_open} -? "Directory not open.";
    -? {! is_empty};
  ]
  (     
    item upper
  );
  
  - parent:ENTRY <-
  (
    get_parent_path path in string_tmp;
    get_entry string_tmp
  );
    
  //
  // Writing:
  //
  
  - make_directory p:ABSTRACT_STRING :DIRECTORY <-
  ( + result:DIRECTORY;    
    string_tmp.copy path;
    string_tmp.add_last '/';
    string_tmp.append p;        
    reduce_path string_tmp;        
    (physical_make_directory string_tmp).if {
      result ?= get_entry string_tmp;            
      ? {result != NULL};
    };
    result
  );
  
  - make_file p:ABSTRACT_STRING :STD_FILE <-
  ( + result:STD_FILE;    
    string_tmp.copy path;
    string_tmp.add_last '/';
    string_tmp.append p;
    reduce_path string_tmp;
    (physical_make_file string_tmp).if {
      result ?= get_entry string_tmp;
      ? {result != NULL};
    };
    result
  );
  
  //
  // Display.
  //
  
  - print <-
  ( 
    lower.to upper do { j:INTEGER;
      item j.path.println;
    };    
  );
  
Section DIRECTORY
  
  //
  // Physical implementation.
  //

  - physical_get_entry new_path:ABSTRACT_STRING :ENTRY <-
  (
    deferred;
    NULL
  );
              
  - physical_make_directory new_path:ABSTRACT_STRING :BOOLEAN <-
  ( 
    deferred;
    FALSE
  );

  - physical_make_file new_path:ABSTRACT_STRING :BOOLEAN <-
  ( 
    deferred;
    FALSE
  );

  - physical_remove p:ABSTRACT_STRING :BOOLEAN <-
  ( 
    deferred;
    FALSE
  );
  
  - physical_move old_path:ABSTRACT_STRING to new_path:ABSTRACT_STRING :BOOLEAN <-
  ( 
    deferred;
    FALSE
  );
  
  //
  // Service routine
  //

  - string_tmp:STRING  := STRING.create 255;
  
Section Private
  
  - string_tmp2:STRING  := STRING.create 255;
