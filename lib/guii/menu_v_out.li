///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Library                                //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name      := MENU_V_OUT;

  - copyright := "Jonathan Ponte, Maxime Audrin, Benoit Sonntag";

  - comment   := "Vertical menu out for GUII.";
  
Section Inherit
  
  - parent_internal_inode:INTERNAL_INODE := INTERNAL_INODE;
  
Section Public
  
 
  - representation:STRING_CONSTANT := "MENU_V_OUT";
  + content:G_WIN_OUT;

  - make_representation_from_prc p:INTEGER <-
  (
    +son_name:STRING;
    +under:G_WIN_OUT;
    +expr:G_EXPR;

    //foreach sons
    (list.lower).to (list.upper) do {
      i:INTEGER;

      son_name:=(list.item i).name;

      //if the son is a MENU_V_OUT or MENU_H_OUT we must connect it to a button
      (((list.item i).parent_internal_inode=MENU_V_OUT) || {((list.item i).parent_internal_inode=MENU_H_OUT)}).if {
        under:=(list.item i).content;
      };

      //buttons or checks whose will compose the MENU_V_OUT pattern
      (i=1).if {
        (i.parent_internal_inode=CHECK).if {
          expr:=CHECK.create (G_OUT.create son_name).fix_width.fix_height;
        } else {
          expr:=G_BUTTON.create (G_OUT.create son_name) connect under.fix_width.fix_height;
        };
      } else {
        (i.parent_internal_inode=CHECK).if {
          expr:=expr / CHECK.create (G_OUT.create son_name).fix_width.fix_height;
        } else {
          expr:=expr / G_BUTTON.create (G_OUT.create son_name) connect under.fix_width.fix_height;
        };
      };
    };

    content:=(G_WIN_OUT.create expr).fix_height.fix_width;
    width:=content.width_min;
    height:=content.height_min;
  );

//
// Evaluation.
//

  - compute_size_from_prc p:INTEGER <-
  [ ? {! list.is_empty}; ]
  (
    +expr:G_EXPR;
    +window:G_WIN_OUT;
    height:=0;
    width:=0;

    list.foreach {
      i:INODE;
      (i.priority > p).if {
        (expr=NULL).if {
          expr:=(G_BUTTON.create (G_OUT.create (i.name))).fix_width.fix_height;
        } else {
          expr:=expr / ((G_BUTTON.create (G_OUT.create (i.name)))).fix_width.fix_height;
        };
      };
    };
    window:=(G_WIN_OUT.create (G_RAW.create expr)).fix_width.fix_height;
    height:=window.height_min;
    width:=window.width_min;
  );


  - semantic_evaluation (w,h:REAL_32) :BOOLEAN<-
  (
    bugw_screen:=w;
    bugh_screen:=h;

    !list.is_empty && {
      browse_group { 
        i:INODE;
        (i.operator != '&')
      }
    } && {
      browse_group {
        i:INODE;
        +tmp:REAL_32;
        +best_prc:REAL_32;

        "MENU_V_OUT evaluation for ".print;i.name.print;"\n".print;
        tmp:=MENU_V_OUT.evaluate i width bugw_screen height bugh_screen;
        (tmp > best_prc).if {
          best_prc:=tmp;
          i.set_representation MENU_V_OUT;
        };
        "CHECK evaluation for ".print;i.name.print;"\n".print;
        tmp:=CHECK.evaluate i width bugw_screen height bugh_screen;
        (tmp > best_prc).if {
          best_prc:=tmp;
          i.set_representation CHECK;
        };
        "WINDOW evaluation for ".print;i.name.print;"\n".print;
        tmp:=WINDOW.evaluate i width bugw_screen height bugh_screen;
        (tmp > best_prc).if {
          best_prc:=tmp;
          i.set_representation WINDOW;
        };
        "MENU_H_OUT evaluation for ".print;i.name.print;"\n".print;
        tmp:=MENU_H_OUT.evaluate i width bugw_screen height bugh_screen;
        (tmp > best_prc).if {
          best_prc:=tmp;
          i.set_representation MENU_H_OUT;
        };
        best_prc > 0
      }
    }
  );

- space_evaluation (w,h:REAL_32) :REAL_32<-
  (
      + r,result:REAL_32;
      // Space evaluation
      compute_size_from_prc 0;
      (height > h).if {
	"Priority 0 failed\n".print;
	compute_size_from_prc 25;
	(height > h).if {
	   "Priority 25 failed\n".print;
	   compute_size_from_prc 50;
	   (height > h).if {
		"Priority 50 failed\n".print;
		r:=0;
		set_height 0;
		set_width 0;
           } else {
		r:=get_nitem_from_prc 50;
	   };
	} else {
	   r:=get_nitem_from_prc 25;
	};
      } else {
	r:=list.count;
     };
     result:=(r/list.count)*100;
     "area := ".print;(get_area_prc (w,h)).print;"\n".print;
     result:=result-(get_area_prc (w,h))/100;
    "result := ".print;result.print;"%\n".print;
    result
  )
  [ ? {Result.in_range 0 to 100}; ];


- evaluate n:INODE width w:REAL_32 height h:REAL_32 : REAL_32<-
  (
    + result:REAL_32;

    // changer le parent de n pour pouvoir utiliser les méthodes spécifiques à MENU_V_OUT
    n.set_representation MENU_V_OUT;

    // Semantic evaluation && Space evaluation.
    "Semantic evaluation of ".print;n.name.print;"\n".print;
    (n.semantic_evaluation (w,h)).if {
      //" OK\n".print;
      "Space evaluation of ".print;n.name.print;"\n".print;
      result:=n.space_evaluation (w,h);
    } else {
      // " FAILED\n".print;
       result:=0;
    };
    "MENU_V_OUT result for ".print;n.name.print;"= ".print;result.print;"%\n".print;
    n.set_representation INTERNAL_INODE;
    result
  )
  [ ? {Result.in_range 0 to 100}; ];


/*
      // -> 1) R = Faisabilité de l'espace (% de recouvrement des items) (1:25%, 2:50%, 3:75%)
      // -> 2) A = Note de calcul d'aire. (% sur l'écran total)
      // Result = (R*100-A)/100  
*/