///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Library                                //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name      := MENU_V_OUT;

  - copyright := "Jonathan Ponte, Maxime Audrin, Benoit Sonntag";

  - comment   := "Vertical menu out for GUII.";
  
Section Inherit
  
  - parent_internal_inode:INTERNAL_INODE := INTERNAL_INODE;
  
Section Public
  
 
  - representation:STRING_CONSTANT := "MENU_V_OUT";
  + win_out:G_WIN_OUT;
  + parent:INTERNAL_INODE;
  - print <-
  (
    "MENU_V_OUT".print;
  );

  - make_representation <-
  (
    +expr:G_EXPR;

    list.foreach {
      i:INODE;

      (i.priority>covering).if {
        i.make_representation;
        (expr=NULL).if {
          expr:=i.content;
        } else {
          expr:=expr / i.content;
        };
      };
    };
    win_out:=(G_WIN_OUT.create expr).fix_height.fix_width;
    (bitmap=NULL).if {
      content:=(G_BUTTON.create (G_OUT.create name) connect win_out).fix_height.fix_width;
    } else {
      content:=(G_BUTTON.create (G_IMG.create bitmap) connect win_out).fix_height.fix_width;
    };
  );

//
// Evaluation.
//

  - compute_size_from_prc p:INTEGER <-
  [ ? {! list.is_empty}; ]
  (
    list.foreach {
      i:INODE;
      (i.priority > p).if {
        (width,height):=G_DIV_EXPR.predict_size (width.rounded,height.rounded) and (G_BUTTON.predict_size (G_OUT.predict_size (i.name)));
      };
    };
    (width,height):=G_WIN_OUT.predict_size (width.rounded,height.rounded);
  );


  - semantic_evaluation (w,h:REAL_32) :BOOLEAN<-
  (
    bugw_screen:=w;
    bugh_screen:=h;

    !list.is_empty && {
      browse_group { 
        i:INODE;
        (i.operator != '&')
      }
    } && {
      +nb:INTEGER;
      list.foreach {
        i:INODE;

        (!i.list.is_empty).if {
          (RAW_V.evaluate i width bugw_screen height bugh_screen > 0).if {
            nb:=nb+1;
          };
        };
      };
      nb=list.count
    }
  );

- space_evaluation (w,h:REAL_32) :REAL_32<-
  (
      + r,result:REAL_32;

      // Space evaluation
      compute_size_from_prc 0;
      ((height > h) || {width > w}).if {
	"Priority 0 failed\n".print;
	compute_size_from_prc 25;
	((height > h) || {width > w}).if {
	   "Priority 25 failed\n".print;
	   compute_size_from_prc 50;
	   ((height > h) || {width > w}).if {
		"Priority 50 failed\n".print;
		r:=0;
		set_height 0;
		set_width 0;
           } else {
		covering:=50;
		r:=get_nitem_from_prc 50;
	   };
	} else {
	   covering:=25;
	   r:=get_nitem_from_prc 25;
	};
      } else {
	covering:=0;
	r:=list.count;
     };
     result:=(r/list.count)*100;
     result:=result-(get_area_prc (w,h));
    result
  )
  [ ? {Result.in_range 0 to 100}; ];


- evaluate n:INODE width w:REAL_32 height h:REAL_32 : REAL_32<-
  (
    + result:REAL_32;


    n.set_representation MENU_V_OUT;
    "MENU_V_OUT semantic evaluation of ".print;n.name.print;"\n".print;
    (n.semantic_evaluation (w,h)).if {
      "MENU_V_OUT space evaluation of ".print;n.name.print;"\n".print;
      result:=n.space_evaluation (w,h);
    } else {
       result:=0;
    };
    "MENU_V_OUT result for ".print;n.name.print;"= ".print;result.print;"%\n".print;
    result
  )
  [ ? {Result.in_range 0 to 100}; ];


/*
      // -> 1) R = Faisabilité de l'espace (% de recouvrement des items) (1:25%, 2:50%, 3:75%)
      // -> 2) A = Note de calcul d'aire. (% sur l'écran total)
      // Result = (R*100-A)/100  
*/