////////////////////////////////////////////////////////////////////////////////
//                            Lisaac Librairy                                 //
//                                                                            //
//                  LSIIT - ULP - CNRS - INRIA - FRANCE                       //
//                                                                            //
//    This program is free software; you can redistribute it and/or modify    //
//    it under the terms of the CeCILL v2 License as published by the         //
//    CEA - CNRS - INRIA.                                                     //
//                                                                            //
//    This program is distributed in the hope that it will be useful,         //
//    but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            //
//    CeCILL v2 License for more details.                                     //
//                                                                            //
//    You should have received a copy of the CeCILL v2 license along with     //
//    this program.                                                           //
//    If not, see <http://www.cecill.info/licences/Licence_CeCILL_V2-en.txt>  //
//                                                                            //
//                    http://isaacproject.u-strasbg.fr/                       //
////////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name    := LINKED2_LIST_NODE[E];

  - comment := "To implement LINKED2_LIST[E].";
    
Section Inherit
  
  - parent_any_two_way_linked_list_node:ANY_TWO_WAY_LINKED_LIST_NODE := ANY_TWO_WAY_LINKED_LIST_NODE;
  
Section Public
  
  + item:E;
  
  + previous:LINKED2_LIST_NODE[E];
  
  + next:LINKED2_LIST_NODE[E];

Section LINKED2_LIST
  
  - create i:E previous p:SELF next n:SELF :SELF<-
  ( + result:SELF;
    
    result := clone;
    result.make i previous p next n;
    result
  );
    
  - make i:E previous p:SELF next n:SELF <-
  (
    item := i;
    previous := p;
    next := n;
  )
  [
    +? {item = i};
    +? {previous = p};
    +? {next = n};
  ];

Section LINKED2_LIST,LINKED2_LIST_NODE
  
  - set_item i:E <-
  (
    item := i;
  )
  [
    +? {item = i};
  ];
    
  - set_next n:SELF <-
  (
    next := n;
  )
  [
    +? {next = n};
  ];
  
  - set_all_with v:E <-
  ( + lnk:SELF;
    
    lnk := Self;    
    {lnk = NULL}.until_do {
      lnk.set_item v;
      lnk := lnk.next;
    };
  );
    
  - set_previous p:SELF <-
  (
    previous := p;
  )
  [
    +? {previous = p};
  ];
