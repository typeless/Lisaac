Section Header
  + name :=LAUNCHER;
  
  - author:="HILBERT Jérôme (hilbertjerome@gmail.com), FUHLHABER Simon(simon.fuhlhaber@gmail.com), Jacquemin Grégoire(greg-jacquemin@hotmail.fr)";
  - comment:="lance le parsing des fichiers du dossier passé en paramètre et écrit dans le fichier svg passé en paramètre(s'il n'éxiste pas il sera créé).";
  
Section Inherit
  
  -parent_object:OBJECT := OBJECT;
  
Section Public
  
  - main <-
  (
    + p:PARSE;
    + reper:ENTRY;
    + string_tmp:STRING;
    + ensfichiers:ENS_FICHIERS;
    + f:FICHIER;
    + svg:SVG_CREATOR;
    + simple:BOOLEAN;
    + uniquement_public:BOOLEAN;
    
    ({COMMAND_LINE.count < 3}||{(COMMAND_LINE.item 1)=="-help"}).if{
      affichage_aide;
    }else{
      svg := SVG_CREATOR.clone;
      f := FICHIER.clone;
      string_tmp := STRING.create 255;
      string_tmp.copy (COMMAND_LINE.item 1);
      "Parsing du dossier: ".print;
      string_tmp.print;
      "\n".print;
      "\n\n\t!!!!ATTENTION A LA MODIFICATION DE STRING.LI !!!!!!!!!!!!!!!!!!!!!!!\n\n\n".print;
      
      //      ENTRY.reduce_path string_tmp;
      
      reper := FILE_SYSTEM.get_entry string_tmp;
      (reper = NULL).if{			
        "Dossier ".print;
        string_tmp.print;
        " inexistant!\n'".print;
        die_with_code exit_failure_code;
      };
      string_tmp.copy (COMMAND_LINE.item 2);
      p := PARSE.clone;
      
      simple := FALSE;
      uniquement_public := FALSE;
      ({COMMAND_LINE.count >=4}&&{(COMMAND_LINE.item 3)=="-simple"}).if{
        simple := TRUE;
      };
      (simple).if_false{
        ({COMMAND_LINE.count >=4}&&{(COMMAND_LINE.item 3)=="-public"}).if{
          uniquement_public := TRUE;
        };
      };
      
      ensfichiers:=p.parcours_dossier reper option simple ou uniquement_public;
      
      svg.make ensfichiers to string_tmp;
      svg.parcours_fichiers;
      
    };
  );
  
Section Private
  - affichage_aide <-
  (
    " \n\nUTILISATION :\n\t1er argument :\trépertoire à parser\n\n\t2ieme argument :destination source SVG\n\n\t(-simple|-public) :\tdiagramme sans méthodes/attributs|avec uniquement les méthodes/attributs public\n\n\n\n".print;
  );
  